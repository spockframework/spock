include "spock-bom"
include "spock-core"
include "spock-specs"
include "spock-specs:mock-integration"
include "spock-spring"
include "spock-spring:spring2-test"
include "spock-spring:spring3-test"
include "spock-guice"

if ((System.getProperty("variant") as BigDecimal ?: 2.4) == 2.4) {
  include "internal-backport"
}

// https://issues.apache.org/jira/projects/TAP5/issues/TAP5-2588
if (JavaVersion.current().isJava7() || JavaVersion.current().isJava8()) {
  include "spock-tapestry"
}

include "spock-unitils"
include "spock-report"
include "spock-gradle"

if (JavaVersion.current().java7Compatible) {
  include "spock-spring:boot-test"
}

if (JavaVersion.current().java8Compatible) {
  include "spock-spring:boot2-test"
  include "spock-spring:spring5-test"
}

rootProject.name = "spock"
nameBuildScriptsAfterProjectNames(rootProject.children)

def nameBuildScriptsAfterProjectNames(projects) {
  for (prj in projects) {
    prj.buildFileName = prj.name + ".gradle" - "spock-"
    nameBuildScriptsAfterProjectNames(prj.children)
  }
}

enableFeaturePreview("STABLE_PUBLISHING")
