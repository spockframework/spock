description = "Spock Framework"

ext {
  libs = [
    ant: "org.apache.ant:ant:1.8.2", // use same version as Groovy 1.8
    asm: "asm:asm:3.2", // use same version as Groovy 1.8
    cglib: "cglib:cglib-nodep:2.2.2",
    easymock: "org.easymock:easymock:3.0",
    groovy: "org.codehaus.groovy:groovy-all:1.8.8",
    h2database: "com.h2database:h2:1.3.164",
    jmock: "org.jmock:jmock:2.5.1",
    jmock_junit4: "org.jmock:jmock-junit4:2.5.1",
    junit: ["junit:junit-dep:4.10@jar", "org.hamcrest:hamcrest-core:1.3"],
    log4j: "log4j:log4j:1.2.16",
    mockito: "org.mockito:mockito-all:1.9.0",
    objenesis: "org.objenesis:objenesis:1.2"
  ]
}

allprojects {
  group = "org.spockframework"
  version = "0.7-groovy-1.8"

  apply from: script("common")
}

apply from: script("ide")

subprojects {
  apply plugin: "groovy"
  apply plugin: "signing"

  sourceCompatibility = 1.5

  repositories {
    mavenCentral()
  }

  configurations {
    all*.exclude module: "junit" // we use junit-dep instead
  }

  dependencies {
    groovy libs.groovy
  }

  signing {
    sign configurations.archives
  }

  signArchives {
    onlyIf { gradle.taskGraph.hasTask(uploadArchives) }
  }

  configureJavadoc(javadoc)

  task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
  }

  artifacts {
    archives sourcesJar, javadocJar
  }
}

task javadoc(type: Javadoc) {
  title "Spock Framework API Documentation ($version)"
  destinationDir file("build/javadoc")
  source subprojects.javadoc.source
  classpath = files(subprojects.javadoc.classpath)
}

configureJavadoc(javadoc)

task publishJavadoc(type: Sync) {
  from javadoc
  into "file:///var/www/spock/javadoc/$version/"
}

task wrapper(type: Wrapper) {
  gradleVersion = "1.2"
}

File script(String name) {
  project.file("gradle/${name}.gradle")
}

def configureJavadoc(task) {
  configure(task) {
    include "spock/**"
    include "org/spockframework/mock/*"
    configure(options) {
      links "http://download.oracle.com/javase/1.5.0/docs/api/"
      links "http://groovy.codehaus.org/gapi"
      links "http://kentbeck.github.com/junit/javadoc/4.10"
      links "http://www.jarvana.com/jarvana/inspect/org/hamcrest/hamcrest-core/1.3/hamcrest-core-1.3-javadoc.jar"
    }
  }
}


