apply plugin: 'osgi'
apply from: script("publishMaven")

ext.displayName = "Spock Framework - Core Module"

description = '''Spock is a testing and specification framework for Java and Groovy applications.
What makes it stand out from the crowd is its beautiful and highly expressive specification language.
Thanks to its JUnit runner, Spock is compatible with most IDEs, build tools, and continuous integration servers.
Spock is inspired from JUnit, jMock, RSpec, Groovy, Scala, Vulcans, and other fascinating life forms.'''

dependencies {
  compile libs.groovy // easiest way to add Groovy dependency to POM
  compile libs.junit

  compile libs.jetbrainsAnnotations, optional
  compile libs.ant, optional
  compile libs.asm, optional
  compile libs.bytebuddy, optional
  compile libs.cglib, optional
  compile libs.objenesis, optional
}

jar {
  manifest {
    name = 'spock-core'
    instruction 'Export-Package', 'org.spockframework.*', 'spock.*'
    instruction 'Embed-Dependency', 'groovy;inline=false', 'junit;inline=false'
    instruction 'Import-Package',
        'org.objenesis;version="[2,3)";resolution:=optional',
        'org.apache.tools.ant;version="[1,2)";resolution:=optional',
        'org.apache.tools.ant.types;version="[1,2)";resolution:=optional',
        'net.bytebuddy;version="[1.6.0,2)";resolution:=optional',
        'net.bytebuddy.dynamic;version="[1.6.0,2)";resolution:=optional',
        'net.bytebuddy.implementation;version="[1.6.0,2)";resolution:=optional',
        'net.bytebuddy.implementation.bind.annotation;version="[1.6.0,2)";resolution:=optional',
        'net.bytebuddy.description.modifier;version="[1.6.0,2)";resolution:=optional',
        'net.sf.cglib.proxy;version="[3.2,4)";resolution:=optional',
        'org.apache.tools.ant.types.selectors;version="[1.7,2)";resolution:=optional',
        'groovy.lang;version="[1.8,4)"', 'org.codehaus.groovy.*;version="[1.8,4)"',
        'org.junit.runner;version="[4,6)"'
  }
}

processResources {
  def tokens = [version: version.toString(), minGroovyVersion: minGroovyVersion, maxGroovyVersion: maxGroovyVersion]
  inputs.property "tokens", tokens
  filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: tokens)
}
