apply from: script("publishMaven")

def modifyBom = { xml ->
  def projectNode = xml.asNode()

  projectNode.remove(projectNode['dependencies'][0]) // remove default dependency section

  projectNode.appendNode('properties').appendNode('spock.version', project.fullVersion)

  def dependencyManagement = projectNode.appendNode('dependencyManagement').appendNode('dependencies')

  def mvnProjects = project.parent.subprojects.findAll { it.plugins.hasPlugin('maven-publish') } // find all published projects
  mvnProjects -= project // don't self reference

  String groupId = project.group
  mvnProjects.name.each {
    dependencyManagement.appendNode('dependency').with { dep ->
      dep.appendNode('groupId', groupId)
      dep.appendNode('artifactId', it)
      dep.appendNode('version', '${spock.version}')
    }
  }
}

ext.displayName = "Spock Framework - Bill of Materials"

description = '''This bill of materials provides managed spock dependencies.'''

publishing.publications.maven {
  pom {
    packaging = 'pom'
    withXml modifyBom
  }
}
