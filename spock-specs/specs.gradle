plugins {
  id 'org.spockframework.base'
  id "org.gradle.test-retry"
}

ext.displayName = "Spock Framework - Specs for Core Module"

description = "Spock specifications for the Core Module. Yes, we eat our own dog food."

configurations {
  groovyConsole
  jacocoAgentRuntime
}

dependencies {
  implementation libs.groovyJmx
  testCompileOnly libs.jetbrainsAnnotations
  testImplementation libs.junitPlatformTestkit

  testImplementation project(":spock-core")
  testImplementation libs.groovySql  //for groovy.sql.Sql
  testImplementation(libs.groovyTest) {  //for groovy.test.NotYetImplemented
    exclude group: 'junit'
  }

  testRuntimeOnly libs.asm
  testRuntimeOnly libs.bytebuddy
  testRuntimeOnly libs.cglib
  testRuntimeOnly libs.objenesis
  testRuntimeOnly libs.h2database

  groovyConsole groovyConsoleExtraDependencies
  jacocoAgentRuntime libs.jacocoAgent
}

boolean isCiServer = System.env["CI"] || System.env["GITHUB_ACTIONS"]
test {
  reports.junitXml.enabled = true
  reports.html.enabled = true

  testLogging.exceptionFormat = "full"
  testLogging.showExceptions = true
  testLogging.events 'standard_out'
  testLogging.events 'standard_error'
  maxParallelForks = 1

  if (isCiServer) {
    retry {
      maxRetries = 2
      maxFailures = 20

      filter {
        includeAnnotationClasses.add("spock.lang.Timeout")
        includeAnnotationClasses.add("spock.lang.Isolated")
      }
    }
  }
}

task groovyConsole(type: JavaExec) {
  main = variant == 2.5 ? "groovy.ui.Console" : "groovy.console.ui.Console"
  classpath = sourceSets.test.runtimeClasspath + configurations.groovyConsole
}

compileTestGroovy {
  def jacocoAgent =objects.newInstance(org.spockframework.gradle.JacocoJavaagentProvider)
  jacocoAgent.jacocoAgent = file(configurations.jacocoAgentRuntime.asPath)
  jacocoAgent.execResultFile = file("$buildDir/jacoco/compileTestGroovy.exec")
  groovyOptions.forkOptions.jvmArgumentProviders.add(jacocoAgent)
}
