import org.springframework.boot.gradle.plugin.SpringBootPlugin

/*
 * Copyright 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  // Normally, the Spring Boot 2 Gradle Plugin would not be compatible with Gradle 9+
  // To get around this issue, we use the dependency-management plugin in isolation
  // as described here: https://docs.spring.io/spring-boot/docs/2.7.7/gradle-plugin/reference/htmlsingle/#managing-dependencies.dependency-management-plugin.using-in-isolation
  // This way, we get proper test execution and realistic version resolution on Gradle 9
  id 'org.springframework.boot' version "2.7.18" apply false
  id "io.spring.dependency-management" version "1.1.7"
}

dependencyManagement {
  imports {
    mavenBom SpringBootPlugin.BOM_COORDINATES
  }
}

// Spring dependency management downgrades the jupiter version to 5.7.2 otherwise
ext['junit-jupiter.version'] = libs.versions.junit.get()

dependencies {
  implementation "org.springframework.boot:spring-boot-starter-data-jpa"
  implementation "org.springframework.boot:spring-boot-starter-web"

  testImplementation "org.springframework.boot:spring-boot-starter-test"
  testImplementation projects.spockCore
  testImplementation projects.spockSpring

  if (javaVersion >= 9) {
    testImplementation libs.jaxb
  }

  runtimeOnly "com.h2database:h2"
}
