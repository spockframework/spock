apply from: script("publishMaven")

ext.displayName = "Spock Framework - Guice Module"

description = "Spock's Guice Module provides support for testing Guice 2/3 based applications."

dependencies {
  api projects.spockCore
  compileOnly "com.google.inject:guice:3.0"
  testImplementation "com.google.inject:guice:3.0"
}

tasks.named("jar", Jar) {
  manifest {
    attributes(
      'Build-Revision': versioning.info.commit,
      'Specification-Title': project.name,
      'Specification-Version': baseVersion,
      'Specification-Vendor': 'spockframework.org',
      'Implementation-Title': project.name,
      'Implementation-Version': variantLessVersion,
      'Implementation-Vendor': 'spockframework.org',
      'Automatic-Module-Name': 'org.spockframework.guice'
    )
  }
}

tasks.named("test", Test).configure {
  if (rootProject.ext.javaVersion >= 17) {
    jvmArgs(
      //Guice Framework requires access to java.lang.ClassLoader.defineClass() from com.google.inject.internal.cglib.core.$ReflectUtils
      "--add-opens=java.base/java.lang=ALL-UNNAMED"
    )
  }
}
