apply plugin: "maven"

basePom = {
  project {
    name project.description
    url "http://spockframework.org"
    description "Spock is a testing and specification framework for Java and Groovy applications."

    licenses {
      license {
        name "The Apache Software License, Version 2.0"
        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
        distribution "repo"
      }
    }

    scm {
      connection "scm:git:git://github.com/spockframework/spock.git"
      developerConnection "scm:git:ssh://git@github.com/spockframework/spock.git"
      url "http://github.spockframework.org"
    }

    developers {
      developer {
        id "pniederw"
        name "Peter Niederwieser"
        email "pniederw@gmail.com"
      }

      developer {
        id "ldaley"
        name "Luke Daley"
        email "ld@ldaley.com"
      }
    }
  }
}

project.poms = []
install {
  project.poms << repositories.mavenInstaller {
    pom basePom
  }.pom
}

uploadArchives {
  project.poms << repositories.mavenDeployer {
    pom basePom
    if (isSnapshotVersion) {
      snapshotRepository(url: "file:///var/www/m2repo/snapshots")
    } else {
      repository(url: "file:///var/www/m2repo/release")
    }
  }.pom
}

modifyPom = { Closure modification ->
  project.poms.each { 
    it.whenConfigured(modification) 
  }
}

def optionalDeps = []

optional = { optionalDeps << it }

modifyPom { pom ->
  optionalDeps.each { dep ->
    pom.dependencies.find { it.artifactId == dep.name }.optional = true
  }
}