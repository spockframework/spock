
pluginManagement {
  plugins {
    id "com.gradle.enterprise" version "3.2.1"
    id "org.asciidoctor.jvm.convert" version "3.2.0"
    id "net.nemerosa.versioning" version "2.13.1"
    id "de.marcphilipp.nexus-publish" version "0.4.0"
    id "com.github.ben-manes.versions" version "0.28.0"
    id "biz.aQute.bnd.builder" version "5.1.2"
  }
}

plugins {
  id "com.gradle.enterprise"
}

gradleEnterprise {
  buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"
  }
}

include "spock-bom"
include "spock-core"
include "spock-specs"
include "spock-specs:mock-integration"
include "spock-spring"
include "spock-spring:spring3-test"
include "spock-guice"
include "spock-junit4"
include "spock-testkit"

if ((System.getProperty("variant") as BigDecimal ?: 2.5) != 3.0) {
  //Remove once Groovy 2.5 support is dropped
  include "spock-groovy2-compat"
} else if (JavaVersion.current().isJava9Compatible()) {
  // works only in Java 9+ and groovy 3+ as spock-groovy2-compat is incompatible with the module system
  include "spock-module-test"
}

// https://issues.apache.org/jira/projects/TAP5/issues/TAP5-2588
if (JavaVersion.current().isJava8()) {
  include "spock-tapestry"
}


include "spock-unitils"
include "spock-gradle"

include "spock-spring:boot-test"
include "spock-spring:boot2-test"
include "spock-spring:spring5-test"

rootProject.name = "spock"
nameBuildScriptsAfterProjectNames(rootProject.children)

def nameBuildScriptsAfterProjectNames(projects) {
  for (prj in projects) {
    prj.buildFileName = prj.name + ".gradle" - "spock-"
    nameBuildScriptsAfterProjectNames(prj.children)
  }
}
