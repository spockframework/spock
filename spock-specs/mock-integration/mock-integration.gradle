ext.displayName = "Spock Framework - Integration Specs for Mocking"

description = "Integration Specs for Mocking"

configurations {
  cglib
  bytebuddy
  objenesis
  mockito4
  mockito5
}

dependencies {
  testImplementation(projects.spockCore) {
    exclude group: "cglib"
    exclude group: "net.bytebuddy"
    exclude group: "org.objenesis"
    exclude group: "org.mockito"
  }
  cglib libs.cglib
  bytebuddy libs.bytebuddy
  objenesis libs.objenesis
  mockito4 libs.mockito4
  mockito5 libs.mockito5
}

def codeGenerationLibraries = [
  cglib: configurations.cglib,
  ByteBuddy: configurations.bytebuddy,
  mockito4 : configurations.mockito4
]

if (rootProject.ext.javaVersion >= 11) {
  //Mockito 5 requires at least Java 11 to run
  codeGenerationLibraries.put("mockito5", configurations.mockito5)
}

codeGenerationLibraries.each { key, config ->
  tasks.register("test${key.capitalize()}WithoutObjenesis", Test) {
    systemProperty("org.spockframework.mock.testType", "${key.toLowerCase()} - objenesis")
    classpath += config

    if (key == "cglib") {
      if (rootProject.ext.javaVersion >= 17) {
        jvmArgs(
          //cglib requires access to java.lang.ClassLoader.defineClass() from net.sf.cglib.core.ReflectUtils
          "--add-opens=java.base/java.lang=ALL-UNNAMED"
        )
      }
      onlyIf { rootProject.ext.javaVersion < 21}
    }
    if (key == "mockito4") {
      // mockito4 only supports up to Java 20
      onlyIf { rootProject.ext.javaVersion < 21}
    }
  }
  tasks.register("test${key.capitalize()}WithObjenesis", Test) {
    systemProperty("org.spockframework.mock.testType", "${key.toLowerCase()} + objenesis")
    classpath += config
    classpath += configurations.objenesis

    if (key == "cglib") {
      if (rootProject.ext.javaVersion >= 17) {
        jvmArgs(
          //cglib requires access to java.lang.ClassLoader.defineClass() from net.sf.cglib.core.ReflectUtils
          "--add-opens=java.base/java.lang=ALL-UNNAMED"
        )
      }
      onlyIf { rootProject.ext.javaVersion < 21}
    }
    if (key == "mockito4") {
      // mockito4 only supports up to Java 20
      onlyIf { rootProject.ext.javaVersion < 21}
    }
  }
}

tasks.named("check") { dependsOn(tasks.withType(Test)) }
