apply from: script("publishMaven")

ext.displayName = "Spock Framework - Unitils Module"

description = "Spock's Unitils Module makes it possible to use Unitils together with Spock."

ext.unitilsVersion = "3.4.6"

dependencies {
  compile project(":spock-core")
  compile "org.unitils:unitils-core:${unitilsVersion}"

  testCompile "org.unitils:unitils-dbunit:${unitilsVersion}", {
    exclude module: "jta"
  }
  testCompile "org.unitils:unitils-database:${unitilsVersion}",  {
    exclude module: "jta"
  }
  testCompile libs.groovySql // for groovy.sql.Sql

  testRuntime libs.h2database
  testRuntime libs.log4j
}

jar {
  manifest {
    attributes(
      'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
      'Build-Date': buildDate,
      'Build-Time': buildTime,
      'Build-Revision': versioning.info.commit,
      'Specification-Title': project.name,
      'Specification-Version': baseVersion,
      'Specification-Vendor': 'spockframework.org',
      'Implementation-Title': project.name,
      'Implementation-Version': variantLessVersion,
      'Implementation-Vendor': 'spockframework.org',
      'Automatic-Module-Name': 'org.spockframework.unitils'
    )
  }
}
