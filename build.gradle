apply plugin: "base"

description = "Spock Framework"

ext {
  libs = [
    ant: "org.apache.ant:ant:1.8.4", // use same version as Groovy 2.0
    asm: "org.ow2.asm:asm:4.0", // use same version as Groovy 2.0
    cglib: "cglib:cglib-nodep:2.2.2",
    easymock: "org.easymock:easymock:3.0",
    groovy: "org.codehaus.groovy:groovy-all:2.0.6",
    h2database: "com.h2database:h2:1.3.164",
    jmock: "org.jmock:jmock:2.5.1",
    jmock_junit4: "org.jmock:jmock-junit4:2.5.1",
    junit: "junit:junit:4.11",
    log4j: "log4j:log4j:1.2.16",
    mockito: "org.mockito:mockito-all:1.9.0",
    objenesis: "org.objenesis:objenesis:1.2"
  ]
}

allprojects {  
  ext.baseVersion = "1.0"
  ext.groovyVersion = "2.0"
  ext.snapshotVersion = true
  
  group = "org.spockframework"
  version = "$baseVersion-groovy-$groovyVersion" + (snapshotVersion ? "-SNAPSHOT" : "")
  ext.groovyLessVersion = baseVersion + (snapshotVersion ? "-SNAPSHOT" : "")
  
  apply from: script("common")
}

apply from: script("ide")

subprojects {
  apply plugin: "groovy"
  apply plugin: "signing"

  sourceCompatibility = 1.5

  repositories {
    mavenCentral()
    maven { url "http://snapshots.repository.codehaus.org" }
  }

  configurations {
    all*.exclude module: "junit-dep"
  }

  dependencies {
    compile(project.name == "spock-gradle" ? [] : libs.groovy)
  }

  signing {
    sign configurations.archives
  }

  signArchives {
    onlyIf { gradle.taskGraph.hasTask(uploadArchives) }
  }

  configureJavadoc(javadoc)
  configureGroovydoc(groovydoc)

  task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
  }

  artifacts {
    archives sourcesJar, javadocJar
  }
}

task javadoc(type: Javadoc) {
  title "Spock Framework API Documentation ($groovyLessVersion)"
  def docPath = snapshotVersion ? "latest" : baseVersion
  destinationDir file("build/javadoc/$docPath")
  source subprojects.javadoc.source
  classpath = files(subprojects.javadoc.classpath)
}

configureJavadoc(javadoc)

task groovydoc(type: Groovydoc) {
  docTitle "Spock Framework API Documentation ($groovyLessVersion)"
  windowTitle "Spock Framework API Documentation ($groovyLessVersion)"
  def docPath = snapshotVersion ? "latest" : baseVersion
  destinationDir file("build/groovydoc/$docPath")
  source subprojects.groovydoc.source
  classpath = files(subprojects.groovydoc.classpath)
  groovyClasspath = project(":spock-core").groovydoc.groovyClasspath
}

task apidoc(dependsOn: ["javadoc", "groovydoc"])

configureGroovydoc(groovydoc)

File script(String name) {
  project.file("gradle/${name}.gradle")
}

def configureJavadoc(task) {
  configure(task) {
    include "spock/**"
    include "org/spockframework/mock/*"
    configure(options) {
      links "http://download.oracle.com/javase/1.5.0/docs/api/"
      links "http://groovy.codehaus.org/gapi"
      links "http://junit-team.github.com/junit/javadoc/4.10" // missing for 4.11
      links "http://hamcrest.org/JavaHamcrest/javadoc/1.3/"
    }
  }
}

def configureGroovydoc(task) {
  configure(task) {
    include "spock/**"
    include "org/spockframework/mock/*"
  }
}

