apply from: script("publishMaven")

ext.displayName = "Spock Framework - Spring Module"

description = "Spock's Spring Module makes it possible to use Spring's TestContext framework together with Spock. \
Supports Spring 2.5.x, 3.x, and 4.x."

def springVersion = "4.3.5.RELEASE"

if (springVersion.startsWith("2.")) {
  sourceSets.test.groovy.exclude "**/*ContextHierarchyExample.groovy"
}

dependencies {
  compile project(":spock-core")
  compile "org.springframework:spring-test:$springVersion", provided
  compile "org.springframework:spring-beans:$springVersion", provided
  compile "org.springframework:spring-context:$springVersion", provided

  // not used directly at compile-time, but needed by groovyc
  testCompile "org.springframework:spring-core:$springVersion"
  testCompile "org.springframework:spring-jdbc:$springVersion"
  testCompile "org.springframework:spring-tx:$springVersion"
  testCompile "javax.inject:javax.inject:1"

  testRuntime libs.h2database
  testRuntime libs.log4j
}

jar {
  manifest {
    attributes(
      'Created-By': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString(),
      'Build-Date': buildDate,
      'Build-Time': buildTime,
      'Build-Revision': versioning.info.commit,
      'Specification-Title': project.name,
      'Specification-Version': baseVersion,
      'Specification-Vendor': 'spockframework.org',
      'Implementation-Title': project.name,
      'Implementation-Version': variantLessVersion,
      'Implementation-Vendor': 'spockframework.org',
      'Automatic-Module-Name': 'org.spockframework.spring'
    )
  }
}
