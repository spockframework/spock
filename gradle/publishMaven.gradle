apply plugin: "maven-publish"
apply plugin: "signing"

def optionalDeps = []
def providedDeps = []

ext {
  packaging = null // use the default ("jar")
  optional = { optionalDeps << it; it }
  provided = { providedDeps << it; it }
}

publishing {
  publications {
    maven(MavenPublication) {
      afterEvaluate {
        pom.packaging = project.ext.packaging
        if (project.ext.packaging != "pom") {
          from(components.java)
          artifact(sourcesJar)
          artifact(javadocJar)
          pom.withXml {
            def dependencies = asNode().dependencies[0]
            optionalDeps.each { dep ->
              dependencies.find { it.artifactId.text() == dep.name }.appendNode("optional", true)
            }
            providedDeps.each { dep ->
              dependencies.find { it.artifactId.text() == dep.name }.scope[0].value = "provided"
            }
          }
        }
      }
      pom {
        name = provider { project.ext.displayName }
        description = provider { project.description }
        url = "http://spockframework.org"
        licenses {
          license {
            name = "The Apache Software License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution = "repo"
          }
        }
        scm {
          connection = "scm:git:git://github.com/spockframework/spock.git"
          developerConnection = "scm:git:ssh://git@github.com/spockframework/spock.git"
          url = "http://github.spockframework.org/spock"
        }
        developers {
          developer {
            id = "pniederw"
            name = "Peter Niederwieser"
            email = "peter@pniederw.com"
          }
          developer {
            id = "ldaley"
            name = "Luke Daley"
            email = "ld@ldaley.com"
          }
        }
      }
    }
  }
  repositories {
    maven {
      def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
      def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      url = snapshotVersion ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = "pniederw"
        password = System.getenv("SONATYPE_OSS_PASSWORD")
      }
    }
  }
}

// ensure all checks pass before publishing
tasks.withType(AbstractPublishToMaven) {
  dependsOn(check)
}

signing {
  sign(publishing.publications).each { task ->
    task.onlyIf { gradle.taskGraph.hasTask(publish) }
  }
}

ext."signing.keyId" = "2EA0A67F"
ext."signing.password" = System.getenv("SIGNING_PASSWORD")
ext."signing.secretKeyRingFile" = "$rootDir/config/code-signing-secring.gpg"
