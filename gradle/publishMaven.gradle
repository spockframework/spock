apply plugin: "de.marcphilipp.nexus-publish"
apply plugin: "signing"

def optionalDeps = []
def providedDeps = []

ext {
  packaging = null // use the default ("jar")
  optional = { optionalDeps << it; it }
  provided = { providedDeps << it; it }
}

publishing {
  publications {
    maven(MavenPublication) {
      afterEvaluate {
        pom.packaging = project.ext.packaging
        if (project.ext.packaging != "pom") {
          from(components.java)
          artifact(sourcesJar)
          artifact(javadocJar)
          pom.withXml {
            def dependencies = asNode().dependencies[0]
            optionalDeps.each { dep ->
              dependencies.find { it.artifactId.text() == dep.name }.appendNode("optional", true)
            }
            providedDeps.each { dep ->
              dependencies.find { it.artifactId.text() == dep.name }.scope[0].value = "provided"
            }
          }
        }
      }
      pom {
        name = provider { project.ext.displayName }
        description = provider { project.description }
        url = "http://spockframework.org"
        licenses {
          license {
            name = "The Apache Software License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution = "repo"
          }
        }
        scm {
          connection = "scm:git:git://github.com/spockframework/spock.git"
          developerConnection = "scm:git:ssh://git@github.com/spockframework/spock.git"
          url = "http://github.spockframework.org/spock"
        }
        developers {
          developer {
            id = "pniederw"
            name = "Peter Niederwieser"
            email = "peter@pniederw.com"
          }
          developer {
            id = "ldaley"
            name = "Luke Daley"
            email = "ld@ldaley.com"
          }
          developer {
            id = "leonard84"
            name = "Leonard BrÃ¼nings"
            email = "lord_damokles@gmx.net"
          }
        }
      }
    }
  }
}

nexusPublishing {
  repositories {
    sonatype {
      username = "pniederw"
      password = System.getenv("SONATYPE_OSS_PASSWORD")
    }
  }
}

// ensure all checks pass before publishing
tasks.withType(AbstractPublishToMaven) {
  dependsOn(check)
}

signing {
  sign(publishing.publications).each { task ->
    task.onlyIf { gradle.taskGraph.hasTask(publish) }
  }
}

ext."signing.keyId" = "2EA0A67F"
ext."signing.password" = System.getenv("SIGNING_PASSWORD")
ext."signing.secretKeyRingFile" = "$rootDir/config/code-signing-secring.gpg"
