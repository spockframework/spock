apply from: script("publishMaven")

ext.displayName = "Spock Framework - Tapestry Module"

description = "Spock's Tapestry Module provides support for testing Tapestry 5 based applications."

def tapestryVersion54 = "5.8.3"

def useTapestry54 = javaVersion >= 8

def tapestryVersionTest = useTapestry54 ? tapestryVersion54 : "5.2.6"

dependencies {
  api projects.spockCore
  compileOnly "org.apache.tapestry:tapestry-ioc:$tapestryVersion54"

  testImplementation "javax.inject:javax.inject:1"
  testImplementation ("org.apache.tapestry:tapestry-ioc"){
    version {
      strictly("$tapestryVersionTest")
    }
  }
}

if (!useTapestry54) {
  tasks.withType(Test).configureEach {
    exclude '**/*WithImportModule.class'
  }
}

tasks.named("jar", Jar) {
  manifest {
    attributes(
      'Build-Revision': versioning.info.commit,
      'Specification-Title': project.name,
      'Specification-Version': baseVersion,
      'Specification-Vendor': 'spockframework.org',
      'Implementation-Title': project.name,
      'Implementation-Version': variantLessVersion,
      'Implementation-Vendor': 'spockframework.org',
      'Automatic-Module-Name': 'org.spockframework.tapestry'
    )
  }
}
